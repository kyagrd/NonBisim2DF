% vim: syntax=bedwyr: sw=2: ts=2: expandtab: ai:
%
% A specification of Modal Logic for pi-calculus.
% Based on the paper
%  "Modal logics for mobile processes", by R. Milner, J. Parrow and D. Walker
%  Theoretical Computer Science 114 (1993).
%
% This definition file implements the specification of MPW modal logic
% described in the paper:
%  Alwen Tiu. Model checking for pi-calculus using proof search.
%  Available online from http://rsise.anu.edu.au/~tiu
%
% Dependency: this file needs the pi.def (definition of late transition
% system for pi-calculus)

% Assume the o'ulas in the modal logic are in negation normal o'.
%
%
% The types of the logical constants:
%
% Formulas of MPW logic are given type o'.
% Propositional connectives:
%  conj : o' -> o' -> o'  (conjunction)
%  disj : o' -> o' -> o'  (disjunction)
%  tt : o'  (true)
%  ff : o' (false)
%
% The prefix 'dia' indicates the 'diamond' modality and 'box' indicates
% the box modality.
% 'Free' modalities:
%   boxMatch : name -> name -> o' -> o'. (match modality, e.g., [x = y] A)
%   diaMatch : name -> name -> o' -> o'. (diamond, e.g., <x=y> A)
%   boxAct, diaAct : action -> o' -> o'. (free actions)
%   boxOut, diaOut : name -> (name -> o') -> o'. (bound output)
%   boxIn, diaIn   : name -> (name -> o') -> o'. (bound input)
%   boxInL, diaInL : name -> (name -> o') -> o'. ('late' bound input)
%   boxInE, diaInE : name -> (name -> o') -> o'. ('early' bound input)

#include "pi.def".
#include "../basic.def".

Kind o'                type.
Type tt, ff            o'.
Type conj, disj           o' -> o' -> o'.
Type boxMatch, boxDiff,       
     diaMatch, diaDiff   name -> name -> o' -> o'.
Type boxAct, diaAct      a -> o' -> o'.
Type boxOut, diaOut,
     boxIn, diaIn,
     boxInL, diaInL,
     boxInE, diaInE      name -> (name -> o') -> o'.

% A "lazy" but incomplete encoding.

Define inductive diff : name -> name -> list (name * name) -> prop
by diff X Y C := _if (X = Y) false (det_mem (X,Y) C).

Define inductive sat : list (name * name) -> p -> o' -> prop
by sat _ _ tt
 ; sat C P (conj A B) := sat C P A /\ sat C P B
 ; sat C P (disj A B) := sat C P A \/ sat C P B
 ; sat C P (boxMatch X Y A) := (X = Y) -> sat C P A
 ; sat C P (diaMatch X Y A) := (X = Y) /\ sat C P A
 ; sat C P (boxDiff X Y A) := sat ((X,Y) :: C) P A
 ; sat C P (diaDiff X Y A) := diff X Y C /\ sat C P A
 ; sat C P (boxAct X A) := forall P1, one P X P1 -> sat C P1 A
 ; sat C P (diaAct X A) := exists P1, one P X P1 /\ sat C P1 A
 ; sat C P (boxOut X A) :=
      forall Q, onep P (up X) Q -> nabla y, sat C (Q y) (A y)
 ; sat C P (diaOut X A) :=
      exists Q, onep P (up X) Q /\ nabla y, sat C (Q y) (A y)
 ; sat C P (boxIn X A) :=
      forall Q, onep P (dn X) Q -> forall y, sat C (Q y) (A y)
 ; sat C P (diaIn X A) :=
      exists Q, onep P (dn X) Q /\ exists y, sat C (Q y) (A y)
 ; sat C P (boxInL X A) :=
      forall Q, onep P (dn X) Q -> exists y, sat C (Q y) (A y)
 ; sat C P (diaInL X A) :=
      exists Q, onep P (dn X) Q /\ forall y, sat C (Q y) (A y)
 ; sat C P (boxInE X A) :=
      exists y, forall Q, onep P (dn X) Q -> sat C (Q y) (A y)
 ; sat C P (diaInE X A) :=
      forall y, exists Q, onep P (dn X) Q /\ sat C (Q y) (A y)
 .


% A complete encoding: using explicit list of names.

Define inductive lsat : list name -> list (name * name) -> p -> o' -> prop
by lsat _ _ _ tt
 ; lsat L C P (conj A B) := lsat L C P A /\ lsat L C P B
 ; lsat L C P (disj  A B) := lsat L C P A \/ lsat L C P B
 ; lsat L C P (boxMatch X Y A) := (X = Y) -> lsat L C P A
 ; lsat L C P (diaMatch X Y A) := (X = Y) /\ lsat L C P A
 ; lsat L C P (boxDiff X Y A) := lsat L ((X,Y) :: C) P A
 ; lsat L C P (diaDiff X Y A) := diff X Y C /\ lsat L C P A
 ; lsat L C P (boxAct X A) := forall P1, one P X P1 -> lsat L C P1 A
 ; lsat L C P (diaAct X A) := exists P1, one P X P1 /\ lsat L C P1 A
 ; lsat L C P (boxOut X A) :=
     forall Q, onep P (up X) Q -> nabla y, lsat (y :: L) C (Q y) (A y)
 ; lsat L C P (diaOut X A) :=
     exists Q, onep P (up X) Q /\ nabla y, lsat (y :: L) C (Q y) (A y)
 ; lsat L C P (boxIn X A) :=
     forall Q, onep P (dn X) Q ->
       nabla w, forall y, member y (w :: L) -> lsat (w :: L) C (Q y) (A y)
 ; lsat L C P (diaIn X A) :=
     exists Q, onep P (dn X) Q /\
       nabla w, exists y, member y (w :: L) /\ lsat (w :: L) C (Q y) (A y)
 ; lsat L C P (boxInL X A) :=
     forall Q, onep P (dn X) Q ->
       nabla w, exists y, member y (w :: L) /\ lsat (w :: L) C (Q y) (A y)
 ; lsat L C P (diaInL X A) :=
     exists Q, onep P (dn X) Q /\
       nabla w, forall y, member y (w :: L) -> lsat (w :: L) C (Q y) (A y)
 ; lsat L C P (boxInE X A) :=
     nabla w, exists y, member y (w :: L) /\
              forall Q, onep P (dn X) Q -> lsat (w :: L) C (Q y) (A y)
 ; lsat L C P (diaInE X A) :=
     nabla w, forall y, member y (w :: L) ->
              exists Q, onep P (dn X) Q /\ lsat (w :: L) C (Q y) (A y)
 .

% Some examples ===============================================================

% Encoding the judgment x(y).0 |= <x(u)>[x = u] false.
Define assert : nat -> o' -> prop by
  % This judgment should be provable in MPW logic
  assert  1 (diaIn x (u\ (boxMatch x u ff)));
  % The 'excluded middle' on names
  assert  2
    (diaInL x (y\ diaInL x (u\ disj (diaMatch y u tt) (boxMatch y u ff))));
  % Process 3 and 4 are strongly bisimilar, hence should satisfy
  % the same set of assertions.
  % <x(y)>^L (<tau><tau> true ; <tau> true).
  assert  3 (diaInL x (y\ disj (diaAct tau (diaAct tau tt))
                            (diaAct tau tt))) ; 
  % Assertions to test tabling behaviours
  assert 4 (boxOut a (m\ boxIn a (x\ boxOut a (n\ boxMatch x m ff)))) ; 
  assert 5 (boxOut a (m\ boxIn a (x\ boxOut a (n\ boxMatch x n ff)))).

Define process : nat -> p -> prop
by process 1 (in x (y\ z))
 ; process 2 (in x (y\ in x u\ z))
 ; process 3 (in x u\ (plus (taup (taup z)) (taup z)))
 ; process 4 (in x u\ nu y\ ((plus (taup (taup z))
                            (plus (taup z)
                                  (taup (match u y (taup z) z))))))
 ; process 5 (nu m\ out a m (in a (x\ nu n\ out a n z)))
 ; process 6 (in x u\ in x v\ (match u v (taup z) z))
 ; process 7 (in x u\ in x y\ taup z)
 .



% Counterexample to the completeness of sat:
Define test : nat -> prop
by % This will generate an exception
   test 1 := process 1 P /\ assert 1 A /\ sat nil P A
   % However, lsat is complete
 ; test 2 := process 1 P /\ assert 1 A /\ lsat (x :: nil) nil P A
   % The excluded middle example is not provable using sat,
   % but is provable using lsat.
 ; test 3 := process 2 P /\ assert 2 A /\ sat nil P A
 ; test 4 := process 2 P /\ assert 2 A /\ lsat (x :: nil) nil P A
 ; test 5 := process 3 P /\ process 4 Q /\ assert 3 A /\ sat nil P A /\ sat nil Q A
   % The next two tests are to test tabling behaviour.
   % Running test 7 after running test 6 should still gives a positive answer. 
 ; test 6 := process 5 P /\ assert 4 A /\ sat nil P A
 ; test 7 := process 5 P /\ assert 5 A /\ sat nil P A
 .

#assert_raise test 1.
#assert test 2.
#assert_not test 3.
#assert test 4.
#assert test 5.
